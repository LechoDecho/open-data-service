plugins {
	id 'org.springframework.boot' version '2.2.0.M5'
	id 'java'
}

def props = new Properties()
file("src/main/resources/application.properties").withInputStream { props.load(it)}

apply plugin: 'io.spring.dependency-management'

group = 'org.jvalue.ods'
version = props.getProperty("app.version")
sourceCompatibility = '12'

task testProps {
	doFirst {
		println version
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/snapshot' }
	maven { url 'https://repo.spring.io/milestone' }
}

test {
	testLogging {
		events "passed", "skipped", "failed"

		afterSuite { desc, result ->
			if (!desc.parent) {
				println "Tests run: ${result.testCount}, " +
						"Failures: ${result.failedTestCount}, " +
						"Skipped: ${result.skippedTestCount}"
			}
		}
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

processResources {
	expand(project.properties)
}
