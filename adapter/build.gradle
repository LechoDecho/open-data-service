plugins {
	id 'org.springframework.boot' version '2.2.0.M2'
	id 'java'
}

apply plugin: 'io.spring.dependency-management'

group = 'org.jvalue.ods'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '12'

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/snapshot' }
	maven { url 'https://repo.spring.io/milestone' }
}

// integration test
sourceSets {
	integrationTest {
		java {
			srcDir file('src/integration-test/java')
		}
		resources.srcDir file('src/integration-test/resources')
	}
}

configurations {
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

task integrationTest(type: Test) {
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	shouldRunAfter test

	testLogging {
		events "passed", "skipped", "failed", "standard_error"
		showExceptions = true

		afterSuite { desc, result ->
			if (!desc.parent) {
				println "Tests run: ${result.testCount}, " +
						"Failures: ${result.failedTestCount}, " +
						"Skipped: ${result.skippedTestCount}"
			}
		}
	}

	check.dependsOn integrationTest
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	integrationTestImplementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.6'
}
